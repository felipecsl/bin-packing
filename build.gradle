plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.2.51'
  id 'maven-publish'
  id 'signing'
}

group 'com.felipecsl'
version '1.0.0-SNAPSHOT'

task sourcesJar(type: Jar) {
  from sourceSets.main.allJava
  classifier = 'sources'
}

task javadocJar(type: Jar) {
  from javadoc
  classifier = 'javadoc'
}

publishing {
  repositories {
    maven {
      def releasesRepoUrl = RELEASE_REPOSITORY_URL
      def snapshotsRepoUrl = SNAPSHOT_REPOSITORY_URL
      url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
      credentials {
        username = NEXUS_USERNAME
        password = NEXUS_PASSWORD
      }
    }
  }
  publications {
    maven(MavenPublication) {
      artifactId = 'bin-packing'
      from components.java
    }
    mavenJava(MavenPublication) {
      artifact sourcesJar
      artifact javadocJar
      pom {
        name = 'Bin Packing'
        description = 'A collection of common bin packing algorithms'
        url = 'https://github.com/felipecsl/bin-packing'
        licenses {
          license {
            name = 'MIT'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'felipecsl'
            name = 'Felipe Lima'
            email = 'felipe.lima@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/felipecsl/bin-packing.git'
          developerConnection = 'scm:git:ssh://git@github.com:felipecsl/bin-packing.git'
          url = 'https://github.com/felipecsl/bin-packing'
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}


javadoc {
  if (JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html4', true)
  }
}

repositories {
  jcenter()
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  implementation "com.google.guava:guava:23.6.1-jre"
  testImplementation 'com.google.truth:truth:0.41'
}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
